# Abreviaciones:
#	RS: Recocido Simulado.
#	FB: Fuerza Bruta.

set encoding iso_8859_1
set terminal epslatex color size 5.88, 3.1

# Directorios actual, de entrada y de salida
gpl_file_dir = system("pwd")
output_dir = "../.."

cd output_dir
input_dir = "./src/data/"	# Relativo a directorio de salida.

# Par치metros
caso = "6x6"

archivo_entrada = input_dir."resultados_".caso.".csv"
archivo_salida = "resultados_".caso.".tex"

color_FB = 1
color_RS = 2

estilo_puntos = 7
estilo_puntos_optimos = 11

titulo = "\\fontsize{13}{13}\\selectfont{Resultados en mallas de 6 $\\times$ 6}"
titulo_eje_x = "\\fontsize{11}{11}\\selectfont{Objetos en malla (cubos, prismas)}"
titulo_eje_y = "\\fontsize{11}{11}\\selectfont{Costo global $T$}"

# El 8.0001 es para evitar bug de de cambio de tama침o de letra en gr치ficas identicas de Beamer.
x_tics_format = "\\fontsize{8.0001}{8.0001}\\selectfont{%s}"
y_tics_format = "\\fontsize{9}{9}\\selectfont{%g}"

x2_tics_format = "\\fontsize{0.6}{0.6}\\selectfont{%s}"
x3_tics_format = "\\fontsize{4.1}{4.1}\\selectfont{$N =$ %g}"

x_tic = ""
x2_tic = ""
sumaPrevia = 1000

keys_font_size = "\\fontsize{8.5}{8.5}\\selectfont"

FB_key = keys_font_size."{B\\'usqueda en muestra}"
RS_key = keys_font_size."{B\\'usqueda por RS}"
costo_optimo_key = keys_font_size."{Costo global \\'optimo}"
coma = keys_font_size."{,}"

key_figs_optimos_x = 0.955
key_figs_optimos_y = 0.812

# Funciones
numCubos(colObjs) = \
	int( \
		substr( \
			strcol(colObjs), \
			0, \
			strstrt(strcol(colObjs), ',') \
		) \
	)
numPrismas(colObjs) = \
	int( \
		substr( \
			strcol(colObjs), \
			strstrt(strcol(colObjs), ',') + 1, \
			strlen(strcol(colObjs)) \
		) \
	)

sumaObjetos(colObjs) = numCubos(colObjs) + numPrismas(colObjs)

estiloPuntosMuestras(colObjs, colCosto) = \
	sumaObjetos(colObjs) == column(colCosto) ? \
	estilo_puntos_optimos : estilo_puntos

estiloPuntosExahust(colObjs, colCosto, colCostoOptimo) = \
	column(colCosto) == column(colCostoOptimo) || \
	sumaObjetos(colObjs) == column(colCosto) ? \
	estilo_puntos_optimos : estilo_puntos

# Marcas mayores ejes x y menores en x2.
decidirColocarMarcaEjesX(x) = ( \
	sumaObjetos(1) == sumaPrevia && \
	!(numCubos(1) == 0 && numPrismas(1) == 19) ? \
	(x_tic = 1/0, x2_tic = sprintf(x2_tics_format, strcol(1))) : \
	(x_tic = sprintf(x_tics_format, strcol(1)), x2_tic = 1/0), \
	sumaPrevia = sumaObjetos(1) \
)

# Marcas mayores eje x2.
decidirColocarMarcaEjeX2(x) = ( \
	sumaObjetos(1) != sumaPrevia || \
	(numCubos(1) == 0 && numPrismas(1) == 19) ? \
	x2_tic = sprintf(x2_tics_format, strcol(1)) : \
	x2_tic = 1/0, \
	sumaPrevia = sumaObjetos(1) \
)

# Marcas $N = X$.
condicion_xtic_1(x) = \
	numCubos(1) == numPrismas(1) || \
	(numCubos(1) == 10 && numPrismas(1) == 9) ? \
	sprintf(x3_tics_format, sumaObjetos(1)) : 1/0

condicion_xtic_2(x) = \
	numCubos(1) - numPrismas(1) == -1 && \
	!(numCubos(1) == 9 && numPrismas(1) == 10) ? \
	sprintf(x3_tics_format, sumaObjetos(1)) : 1/0

# ==============================================================================================================================

set output archivo_salida
stats archivo_entrada nooutput

set multiplot layout 1,1

set margins 3.7, 0.1, 2.3, 1.5

set border
set border lw 1.2

set xrange [-2:424]
set yrange [15:124]

set label titulo center at graph 0.5, screen 1 offset 0, char -0.5
set label titulo_eje_x center at graph 0.5, screen 0 offset 0, char 0.5
set label titulo_eje_y center at screen 0, graph 0.5 rotate by 90 offset char 0.5, 0

set xtics scale 1
set xtics mirror
set xtics offset 0, 0.5

set x2tics scale 0.5
set x2tics offset -0.54, -0.55
set x2tic rotate by 90

set ytics right 20, 10, 110
set format y y_tics_format
set mytics 5
set ytics add right ("\\raisebox{2pt}{\\fontsize{9}{9}\\selectfont{$\\infty$}}" 120)
set ytics add right ("" 18 1)
set ytics right offset 0.7, 0

set key at graph 0.998, 0.97
set key samplen 1.5
set key spacing 1

# Leyenda de 칩ptimos.
set label at graph key_figs_optimos_x, key_figs_optimos_y "" point pt estilo_puntos_optimos ps 1.2 lc color_FB
set label at graph key_figs_optimos_x + 0.0187, key_figs_optimos_y "" point pt estilo_puntos_optimos ps 1.2 lc color_RS
set label at graph key_figs_optimos_x + 0.006, key_figs_optimos_y + 0.005 coma

plot \
	archivo_entrada u 0:3:(estiloPuntosMuestras(1, 3)) w lp lw 1 lc 1 pt variable ps 0.5 t "", \
	archivo_entrada u 0:2:(decidirColocarMarcaEjesX(NaN)):(estiloPuntosMuestras(1, 2)): \
		xtic(x_tic):x2tic(x2_tic) \
		w lp lw 1 lc 2 pt variable ps 0.5 t "", \
	keyentry w lp lw 2 lc color_FB pt estilo_puntos ps 1.2 t FB_key, \
	keyentry w lp lw 2 lc color_RS pt estilo_puntos ps 1.2 t RS_key, \
	keyentry title costo_optimo_key

set border 0
unset label
unset xtics
unset ytics
unset key

# Nombres de marcas mayores en eje x2.
sumaPrevia = 1000
x2_tic = ""

set x2tics scale 0

plot archivo_entrada u 0:(NaN):(decidirColocarMarcaEjeX2(NaN)):x2tic(x2_tic) lw 0 ps 0 t ""

# Nombres $N = X$ en eje x inferior.
unset x2tics

set xtics scale 0
set xtics offset first 0.4, char 1.45

plot archivo_entrada u 0:(NaN):xtic(condicion_xtic_1(NaN)) t ""

set xtics offset 0, char 1.45

plot archivo_entrada u 0:(NaN):xtic(condicion_xtic_2(NaN)) t ""

unset multiplot
unset output

cd gpl_file_dir
reset