# Abreviaciones:
#	RS: Recocido Simulado.
#	FB: Fuerza Bruta.

set encoding iso_8859_1
set terminal epslatex color size 5.7, 3.1

# Directorios actual, de entrada y de salida 
gpl_file_dir = system("pwd")
output_dir = "../.."

cd output_dir
input_dir = "./src/data/"	# Relativo a directorio de salida.

# Parámetros
caso = "3x3"

archivo_entrada = input_dir."resultados_".caso.".csv"
archivo_salida = "resultados_".caso.".tex"

color_FB = 1
color_RS = 2

estilo_puntos = 7
estilo_puntos_optimos = 11

titulo = "\\fontsize{13}{13}\\selectfont{Resultados en mallas de 3 $\\times$ 3}"
titulo_eje_x = "\\fontsize{12}{12}\\selectfont{Objetos en malla (cubos, prismas)}"
titulo_eje_y = "\\fontsize{12}{12}\\selectfont{Costo global $T$}"

x_tics_format = "\\fontsize{7}{7}\\selectfont{%s}"
y_tics_format = "{\\fontsize{9}{9}\\selectfont{%g}}"

keys_font_size = "\\fontsize{8.5}{8.5}\\selectfont"

FB_key = keys_font_size."{B\\'usqueda exhaustiva}"
RS_key = keys_font_size."{B\\'usqueda por RS}"
costo_optimo_key = keys_font_size."{Costo global \\'optimo}"
coma = keys_font_size."{,}"

# Funciones

numCubos(colObjs) = int(substr(strcol(colObjs), 0, strstrt(strcol(colObjs), ',')))
numPrismas(colObjs) = int(substr(strcol(colObjs), strstrt(strcol(colObjs), ',') + 1, strlen(strcol(colObjs))))
sumaObjetos(colObjs) = numCubos(colObjs) + numPrismas(colObjs)

estiloPuntosMuestras(colObjs, colCosto) = \
	sumaObjetos(colObjs) == column(colCosto) ? \
	estilo_puntos_optimos : estilo_puntos

estiloPuntosExahust(colObjs, colCosto, colCostoOptimo) = \
	column(colCosto) == column(colCostoOptimo) || \
	sumaObjetos(colObjs) == column(colCosto) ? \
	estilo_puntos_optimos : estilo_puntos

# ==============================================================================================================================

set output archivo_salida

set multiplot layout 1,1

set margins 4, 0.1, 2.7, 1.8

set border
set border lw 1.2

set xrange [-1:32]
set yrange [4:23]

set label titulo center at graph 0.5, screen 1 offset 0, char -0.75

set label titulo_eje_x center at graph 0.5, screen 0 offset 0, char 0.5
set label titulo_eje_y center at screen 0, graph 0.5 rotate by 90 offset char 0.5, 0

set xtics offset -0.5, -0.3
set xtic rotate by 45

set ytics offset -0.15, 0
set format y y_tics_format
set mytics 2

set key samplen 1.45
set key at graph 0.99, 0.97
set key spacing 1

# Leyenda de óptimos.
set label at graph 0.946, 0.806 "" point pt estilo_puntos_optimos ps 1.2 lc color_FB
set label at graph 0.966, 0.806 "" point pt estilo_puntos_optimos ps 1.2 lc color_RS
set label at graph 0.953, 0.811 coma

# El archivo "resultados_3x3.txt" fue generado manualmente a partir de "limites_acciones_3x3.txt" y "datos_resultados_3x3.txt".
plot \
	archivo_entrada u 0:3 w lp lw 1.6 lc color_FB pt estilo_puntos_optimos ps 1.9 t "", \
	archivo_entrada u 0:2:(estiloPuntosExahust(1, 2, 3)):xtic(sprintf(x_tics_format, strcol(1))) w lp lw 1.6 lc color_RS pt variable ps 1.9 t "", \
	keyentry w lp lw 2 lc color_FB pt estilo_puntos_optimos ps 1.2 t FB_key, \
	keyentry w lp lw 2 lc color_RS pt estilo_puntos ps 1.2 t RS_key, \
	keyentry title costo_optimo_key

unset multiplot
unset output

cd gpl_file_dir
reset